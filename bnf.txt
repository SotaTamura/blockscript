<program>             ::= (<expression> ";"?)*

<expression>          ::= <if_statement>
                        | <while_statement>
                        | <for_statement>
                        | <assignment>

<if_statement>        ::= "if" "(" <expression> ")" <expression> ("else" <expression>)?
<while_statement>     ::= "while" "(" <expression> ")" <expression>
<for_statement>       ::= "for" "(" <identifier> "in" <expression> ")" <expression>

<assignment>          ::= <logical_or> ("=" <expression>)?

<logical_or>          ::= <logical_and> ("|" <logical_and>)*
<logical_and>         ::= <relational> ("&" <relational>)*
<relational>          ::= <additive> (( "==" | "!=" | "<" | "<=" | ">" | ">=" ) <additive>)?
<additive>            ::= <multiplicative> (( "+" | "-" ) <multiplicative>)*
<multiplicative>      ::= <unary> (( "*" | "/" | "%" ) <unary>)*

<unary>               ::= ( "!" | "-" ) <unary>
                        | <postfix>

<postfix>             ::= <factor> ( <call_args> | <index_access> | <property_access> )*
<call_args>           ::= "(" (<expression> ("," <expression>)* ","?)? ")"
<index_access>        ::= "[" <expression> "]"
<property_access>     ::= "." <identifier>

<factor>              ::= "(" <program> ")"
                        | "{" <object_literal> "}"
                        | <list_literal>
                        | <identifier>
                        | "return" <expression>?
                        | "break" <expression>?
                        | "continue" <expression>?
                        | <function_definition>
                        | <number>
                        | <string>
                        | <boolean>
                        | "this"
                        | "undefined"

<object_literal>      ::= (<identifier> ":" <expression> ("," <identifier> ":" <expression>)* ","?)?
<list_literal>        ::= "[" (<expression> ("," <expression>)* ","?)? "]"
<function_definition> ::= "function" "(" (<identifier> ("," <identifier>)* ","?)? ")" <expression>

<identifier>            ::= [a-zA-Z_][a-zA-Z0-9_]*
<number>                ::= [0-9]+
<string>                ::= '"' [^"]* '"'
<boolean>               ::= "true" | "false"
